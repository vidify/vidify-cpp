# Try to find DBus
# Once done, this will define
#
#  DBUS_FOUND - system has DBus
#  DBUS_INCLUDE_DIRS - the DBus include directories
#  DBUS_LIBRARIES - link these to use DBus

FIND_PACKAGE(PkgConfig)
PKG_CHECK_MODULES(PC_DBUS QUIET dbus-1)

FIND_LIBRARY(DBUS_LIBRARIES
    NAMES dbus-1
    HINTS ${PC_DBUS_LIBDIR}
          ${PC_DBUS_LIBRARY_DIRS}
)
IF (DBUS_LIBRARIES)
    MESSAGE(STATUS "Found DBus library path: ${DBUS_LIBRARIES}")
ENDIF (DBUS_LIBRARIES)

FIND_PATH(DBUS_INCLUDE_DIR
    NAMES dbus/dbus.h
    HINTS ${PC_DBUS_INCLUDEDIR}
          ${PC_DBUS_INCLUDE_DIRS}
)
IF (DBUS_INCLUDE_DIR)
    MESSAGE(STATUS "Found DBus include-dir path: ${DBUS_INCLUDE_DIR}")
ENDIF (DBUS_INCLUDE_DIR)

GET_FILENAME_COMPONENT(_DBUS_LIBRARY_DIR ${DBUS_LIBRARIES} PATH)

FIND_PATH(DBUS_ARCH_INCLUDE_DIR
    NAMES dbus/dbus-arch-deps.h
    HINTS ${PC_DBUS_INCLUDEDIR}
          ${PC_DBUS_INCLUDE_DIRS}
          ${_DBUS_LIBRARY_DIR}
          ${DBUS_INCLUDE_DIR}
    PATH_SUFFIXES include
)
IF (DBUS_ARCH_INCLUDE_DIR)
    MESSAGE(STATUS "Found DBus arch-include-dir path: ${DBUS_ARCH_INCLUDE_DIR}")
ENDIF (DBUS_ARCH_INCLUDE_DIR)

SET(DBUS_INCLUDE_DIRS ${DBUS_INCLUDE_DIR} ${DBUS_ARCH_INCLUDE_DIR})

INCLUDE(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(DBUS REQUIRED_VARS DBUS_INCLUDE_DIRS DBUS_LIBRARIES)
